# Usage:
# source iterm2.zsh

# from https://gist.github.com/wadey/1140259

# iTerm2 tab color commands
# https://iterm2.com/documentation-escape-codes.html

if [[ -n "$ITERM_SESSION_ID" ]]; then
    tab-color() {
        echo -ne "\033]6;1;bg;red;brightness;$1\a"
        echo -ne "\033]6;1;bg;green;brightness;$2\a"
        echo -ne "\033]6;1;bg;blue;brightness;$3\a"
    }
    tab-red() { tab-color 255 0 0 }
    tab-green() { tab-color 0 255 0 }
    tab-blue() { tab-color 0 0 255 }
    tab-reset() { echo -ne "\033]6;1;bg;*;default\a" }

    function iterm2_tab_precmd() {
        tab-reset
    }

    function iterm2_tab_preexec() {
        if [[ "$1" =~ "^ssh " ]]; then
            if [[ "$1" =~ "preprod" ]]; then
                tab-color 160 160 255
            elif [[ "$1" =~ "prod" ]]; then
                tab-color 255 100 100
            elif [[ "$1" =~ "dev" ]]; then
                tab-color 160 255 160
            else
                tab-color 160 160 255
            fi
        elif [[ "$1" =~ "vagrant ssh" ]]; then
            tab-color 160 255 160
        #else
        #    tab-color 160 160 255
        fi
    }

    autoload -U add-zsh-hook
    add-zsh-hook precmd  iterm2_tab_precmd
    add-zsh-hook preexec iterm2_tab_preexec
fi

